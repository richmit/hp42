LBL "BASE"
LBL 01
CLMENU
"S&R"
KEY 1 GTO 04
"BITS"
KEY 2 GTO 05
"FUN"
KEY 3 GTO 06
"MASK"
KEY 4 GTO 07
"ARTH"
KEY 5 GTO 08
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 7 GTO 03
KEY 8 GTO 02
KEY 9 GTO 00
MENU
STOP
GTO 01
LBL 02
CLMENU
XEQ 92
KEY 1 XEQ 09
XEQ 93
KEY 2 XEQ 10
XEQ 94
KEY 3 XEQ 11
XEQ 95
KEY 4 XEQ 12
"BMNU"
KEY 6 XEQ 75
KEY 7 GTO 01
KEY 8 GTO 03
KEY 9 GTO 00
MENU
STOP
GTO 02
LBL 03
CLMENU
XEQ 96
KEY 1 XEQ 13
XEQ 97
KEY 2 XEQ 14
XEQ 98
KEY 3 XEQ 15
XEQ 99
KEY 5 XEQ 74
KEY 7 GTO 02
KEY 8 GTO 01
KEY 9 GTO 00
MENU
STOP
GTO 03
LBL 04
CLMENU
"SLn"
KEY 1 XEQ "SLn"
"SRn"
KEY 2 XEQ "SRn"
"RLn"
KEY 3 XEQ "RLn"
"RRn"
KEY 4 XEQ "RRn"
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 7 GTO 17
KEY 8 GTO 16
KEY 9 GTO 01
MENU
STOP
GTO 04
LBL 16
CLMENU
"LJ"
KEY 1 XEQ "LJ"
"RJ"
KEY 2 XEQ "RJ"
"SHFXY"
KEY 3 XEQ "SHFXY"
"ROTXY"
KEY 4 XEQ 18
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 7 GTO 04
KEY 8 GTO 17
KEY 9 GTO 01
MENU
STOP
GTO 16
LBL 17
CLMENU
"SL"
KEY 1 XEQ "SL"
"SR"
KEY 2 XEQ "SR"
"RL"
KEY 3 XEQ "RL"
"RR"
KEY 4 XEQ "RR"
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 7 GTO 16
KEY 8 GTO 04
KEY 9 GTO 01
MENU
STOP
GTO 17
LBL 05
CLMENU
"GETB"
KEY 1 XEQ "GETB"
"SB"
KEY 2 XEQ "SETB"
"CB"
KEY 3 XEQ "CLRB"
"AB"
KEY 4 XEQ "ASNB"
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 7 GTO 20
KEY 8 GTO 19
KEY 9 GTO 01
MENU
STOP
GTO 05
LBL 19
CLMENU
"GY@X"
KEY 1 XEQ "GY@X"
"SY@X"
KEY 2 XEQ "SY@X"
"CY@X"
KEY 3 XEQ "CY@X"
"AZ@Y"
KEY 4 XEQ "AZ@Y"
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 7 GTO 05
KEY 8 GTO 20
KEY 9 GTO 01
MENU
STOP
GTO 19
LBL 20
CLMENU
"POPRB"
KEY 1 XEQ "POPRB"
"POPLB"
KEY 2 XEQ "POPLB"
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 7 GTO 19
KEY 8 GTO 05
KEY 9 GTO 01
MENU
STOP
GTO 20
LBL 06
CLMENU
"B#"
KEY 1 XEQ "B#"
"MSSB"
KEY 2 XEQ "MSSB"
"LSSB"
KEY 3 XEQ "LSSB"
"ILN2"
KEY 4 XEQ "ILN2"
"ISQRT"
KEY 5 XEQ "ISQRT"
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 7 GTO 21
KEY 8 GTO 21
KEY 9 GTO 01
MENU
STOP
GTO 06
LBL 21
CLMENU
"REVNB"
KEY 1 XEQ "REVNB"
"REVB"
KEY 2 XEQ "REVB"
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 7 GTO 06
KEY 8 GTO 06
KEY 9 GTO 01
MENU
STOP
GTO 21
LBL 07
CLMENU
"MSKL"
KEY 1 XEQ "MSKL"
"MSKR"
KEY 2 XEQ "MSKR"
"MSKn"
KEY 3 XEQ "MSKn"
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 9 GTO 01
MENU
STOP
GTO 07
LBL 08
CLMENU
"AND"
KEY 1 XEQ 23
"OR"
KEY 2 XEQ 24
"XOR"
KEY 3 XEQ 25
"NOT"
KEY 4 XEQ 26
"NEG"
KEY 5 XEQ 27
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 7 GTO 22
KEY 8 GTO 22
KEY 9 GTO 01
MENU
STOP
GTO 08
LBL 22
CLMENU
"+"
KEY 1 XEQ 28
"-"
KEY 2 XEQ 29
"×"
KEY 3 XEQ 30
"÷"
KEY 4 XEQ 31
"MOD"
KEY 5 XEQ 32
"BVIEW"
KEY 6 XEQ "BVIEW"
KEY 7 GTO 08
KEY 8 GTO 08
KEY 9 GTO 01
MENU
STOP
GTO 22
LBL 00
EXITALL
RTN
LBL 09
HEXM
RTN
LBL 10
DECM
RTN
LBL 11
OCTM
RTN
LBL 12
BINM
RTN
LBL 13
WSIZE
RTN
LBL 14
BSIGNED
RTN
LBL 15
BWRAP
RTN
LBL 18
ROTXY
RTN
LBL 23
AND
RTN
LBL 24
OR
RTN
LBL 25
XOR
RTN
LBL 26
NOT
RTN
LBL 27
BASE+/-
RTN
LBL 28
BASE+
RTN
LBL 29
BASE-
RTN
LBL 30
BASE×
RTN
LBL 31
BASE÷
RTN
LBL 32
MOD
RTN
LBL "B#"
FUNC 11
L4STK
IP
0
X<>Y
1
NOT
X<>Y
LBL 70
RCL ST Y
X<>Y
AND
LASTX
X=Y?
GTO 71
R↓
1
STO+ ST T
LBL 71
R↓
X=0?
GTO 72
X<>Y
-1
ROTXY
X<>Y
GTO 70
LBL 72
R↓
R↓
RTN
LBL "MSKn"
FUNC 21
L4STK
X<>Y
XEQ "MSKR"
X<>Y
XEQ "SLn"
RTN
LBL "MSKL"
FUNC 11
L4STK
XEQ "MSKR"
XEQ "LJ"
R↓
RTN
LBL "MSKR"
FUNC 11
L4STK
IP
0
0≥? ST Y
RTN
R↓
0
NOT
XEQ 67
RTN
R↓
2
X<>Y
Y↑X
1
-
RTN
LBL "SETB"
FUNC 21
L4STK
IP
X<>Y
0>? ST Y
RTN
XEQ 67
RTN
X<>Y
1
X<>Y
+/-
ROTXY
OR
RTN
LBL "CLRB"
FUNC 21
L4STK
IP
X<>Y
IP
0>? ST Y
RTN
XEQ 67
RTN
X<>Y
1
X<>Y
+/-
ROTXY
NOT
AND
RTN
LBL "GETB"
FUNC 21
L4STK
0
0>? ST Y
RTN
XEQ 67
RTN
R↓
BIT?
GTO 69
0
RTN
LBL 69
1
RTN
LBL "ASNB"
FUNC 31
L4STK
X=0?
GTO 54
R↓
XEQ "SETB"
RTN
LBL 54
R↓
XEQ "CLRB"
RTN
LBL "GY@X"
FUNC 31
L4STK
RCL ST Z
X<>Y
XEQ "SRn"
X<>Y
XEQ "MSKR"
AND
RTN
LBL "CY@X"
FUNC 31
L4STK
XEQ "MSKn"
NOT
AND
RTN
LBL "SY@X"
FUNC 31
L4STK
XEQ "MSKn"
OR
RTN
LBL "AZ@Y"
FUNC 41
L4STK
LSTO "_X"
R↓
LSTO "_Y"
R↓
LSTO "_Z"
RCL "_Y"
XEQ "MSKn"
NOT
AND
RCL "_X"
RCL "_Z"
XEQ "MSKR"
AND
RCL "_Y"
XEQ "SLn"
OR
RTN
LBL "SL"
FUNC 11
L4STK
-1
ROTXY
1
NOT
AND
RTN
LBL "SR"
FUNC 11
L4STK
1
NOT
AND
1
ROTXY
RTN
LBL "RL"
FUNC 11
L4STK
-1
ROTXY
RTN
LBL "RR"
FUNC 11
L4STK
1
ROTXY
RTN
LBL "SLn"
FUNC 21
L4STK
IP
X<>Y
IP
0≥? ST Y
RTN
X<>Y
0
XEQ 67
RTN
R↓
X<>Y
RCL ST Y
+/-
ROTXY
2
RCL ST Z
Y↑X
1
-
NOT
AND
RTN
LBL "SRn"
FUNC 21
L4STK
IP
X<>Y
IP
0≥? ST Y
RTN
X<>Y
0
XEQ 67
RTN
R↓
X<>Y
RCL ST Y
2
X<>Y
Y↑X
1
-
NOT
AND
X<>Y
ROTXY
RTN
LBL "RLn"
FUNC 21
L4STK
IP
X<>Y
0≥? ST Y
RTN
X<>Y
+/-
ROTXY
RTN
LBL "RRn"
FUNC 21
L4STK
IP
X<>Y
0≥? ST Y
RTN
X<>Y
ROTXY
RTN
LBL "SHFXY"
FUNC 21
L4STK
IP
X<0?
GTO 59
XEQ "SRn"
RTN
LBL 59
+/-
XEQ "SLn"
RTN
LBL "RJ"
FUNC 12
L4STK
IP
X=0?
RTNERR 4
0
X<>Y
LBL 57
0
BIT?
GTO 56
GTO 58
LBL 56
R↓
X<>Y
RTN
LBL 58
R↓
1
STO+ ST Z
ROTXY
GTO 57
LBL "LJ"
FUNC 12
L4STK
IP
X=0?
RTNERR 4
0
X<>Y
LBL 64
WSIZE?
1
-
BIT?
GTO 65
GTO 66
LBL 65
R↓
X<>Y
RTN
LBL 66
R↓
1
STO+ ST Z
+/-
ROTXY
GTO 64
LBL "ISQRT"
FUNC 11
L4STK
ABS
SQRT
IP
RTN
LBL "ILN2"
FUNC 11
L4STK
ABS
LN
2
LN
÷
IP
RTN
LBL "MSSB"
FUNC 11
L4STK
IP
-1
0=? ST Y
RTN
R↓
XEQ "LJ"
1
+
WSIZE?
X<>Y
-
RTN
LBL "LSSB"
FUNC 11
L4STK
IP
-1
0=? ST Y
RTN
R↓
XEQ "RJ"
RTN
LBL 96
"W:"
WSIZE?
AIP
R↓
RTN
LBL 97
FS? 78
"S:+/-"
FC? 78
"S:+"
RTN
LBL 98
FS? 79
"BWR•"
FC? 79
"BWRP"
RTN
LBL 99
FC? 02
"BVP•"
FS? 02
"BVP"
RTN
LBL 92
"HEX"
FS? 71
├"•"
RTN
LBL 93
"DEC"
SF 81
FS? 71
CF 81
FS? 70
CF 81
FS? 68
CF 81
FS? 81
├"•"
RTN
LBL 94
"OCT"
SF 81
FS? 71
CF 81
FC? 70
CF 81
FS? 81
├"•"
RTN
LBL 95
"BIN"
SF 81
FC? 68
CF 81
FS? 69
CF 81
FS? 81
├"•"
RTN
LBL 74
FS?C 02
RTN
SF 02
RTN
LBL 75
EXITALL
"Press R/S To"
├" Return"
AVIEW
HEXM
STOP
RTN
LBL 67
FUNC 00
L4STK
X<>Y
WSIZE?
X>Y?
RTNNO
RTNYES
LBL "REVNB"
FUNC 11
L4STK
ENTER
XEQ "MSKR"
X<>Y
1
-
LSTO "_MBC"
1000
÷
LSTO "_CTR"
R↓
RCL ST Y
OR
X<>Y
LBL 51
RCL "_CTR"
IP
BIT?
GTO 52
R↓
X<>Y
RCL "_CTR"
IP
RCL- "_MBC"
1
X<>Y
ROTXY
NOT
AND
X<>Y
ENTER
LBL 52
R↓
ISG "_CTR"
GTO 51
X<>Y
RTN
LBL "REVB"
FUNC 11
L4STK
WSIZE?
XEQ "REVNB"
RTN
LBL "POPRB"
FUNC 22
L4STK
RCL ST Y
RCL ST Y
XEQ "SRn"
RCL ST Z
RCL ST Z
XEQ "MSKR"
AND
RTN
LBL "POPLB"
FUNC 22
L4STK
RCL ST Y
RCL ST Y
XEQ "SLn"
RCL ST Z
RCL ST Z
XEQ "MSKL"
AND
RCL ST Z
+/-
ROTXY
RTN
LBL "BVIEW"
FUNC 11
L4STK
0
SF 25
BIT?
FS?C 25
GTO 79
R↓
"ERR: Bad Int!"
AVIEW
RTN
LBL 79
R↓
LSTO "_X"
20
1
FS? 68
STO+ ST Y
FS? 70
STO+ ST Y
FS? 71
STO+ ST Y
R↓
LSTO "_SBMD"
R↓
CF 81
SF 25
RCL "GrMod"
FS?C 25
SF 81
FC? 81
GTO 88
0
STO "GrMod"
R↓
LBL 88
CLLCD
EXITALL
63
1000
÷
LSTO "_CTR"
LBL 76
RCL "_CTR"
IP
32
X>Y?
GTO 73
GTO 83
LBL 73
1
RCL "_CTR"
4
×
1
+
GTO 90
LBL 83
9
RCL "_CTR"
32
-
4
×
1
+
LBL 90
63
RCL "_CTR"
IP
-
WSIZE?
X>Y?
GTO 80
GTO 81
LBL 80
R↓
+/-
1
X<>Y
ROTXY
RCL "_X"
AND
X=0?
GTO 85
GTO 86
LBL 85
R↓
0
GTO 87
LBL 86
R↓
1
LBL 87
LSTO "_CB"
GTO 82
LBL 81
R↓
R↓
0
LSTO "_CB"
FS? 02
GTO 84
LBL 82
R↓
RCL "_CTR"
IP
4
÷
IP
2
÷
FP
X=0?
GTO 55
GTO 68
LBL 55
R↓
0
GTO 77
LBL 68
R↓
2
LBL 77
RCL+ "_CB"
XEQ 78
LBL 84
ISG  "_CTR"
GTO 76
FC? 81
GTO 89
LBL 89
RCL "_SBMD"
20
X=Y?
DECM
R↓
21
X=Y?
BINM
R↓
22
X=Y?
OCTM
R↓
23
X=Y?
HEXM
R↓
RCL "_X"
RTN
LBL 78
FUNC 30
L4STK
IP
60
+
XEQ IND ST X
R↓
AGRAPH
RTN
LBL 60
"•μ•"
RTN
LBL 61
"£•←"
RTN
LBL 62
"_Q_"
RTN
LBL 63
"R_P"
RTN
END

